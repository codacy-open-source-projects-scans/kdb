m4_define([E2E_RUN_PIPE],[[
$1 | libtool --mode=execute -- "$abs_srcdir"/strace.sh $2 || exit $?;
]])

m4_define([E2E_RUN],[[
libtool --mode=execute -- "$abs_srcdir"/strace.sh $1 || exit $?
]])

m4_define([E2E_RUN_MEMCHECK],[[
libtool --mode=execute -- "$abs_srcdir"/valgrind.sh memcheck $1 || exit $?
]])

m4_define([E2E_MEMCHECK],[
AT_CHECK([E2E_RUN_MEMCHECK([$1])], [0], [], [], [$2], [$3])
])

m4_define([E2E_CHECK_PIPE],[
AT_CHECK([E2E_RUN_PIPE([$1], [$2])], [0], [], [], [$3], [$4])
])

m4_define([E2E_CHECK],[
AT_CHECK([E2E_RUN([$1])], [0], [], [], [$2], [$3])
])

m4_define([E2E_CHECK_FAIL],[
AT_CHECK([E2E_RUN([$1])], [$2], [], [], [$3], [$4])
])

m4_define([E2E_COMPARE_CONTENT],[
$1 > "$2.expect";
ln -f -s -- "$2.expect" expout;
AT_CHECK([[cat -- "$2"]], [0], [expout], [])
])

m4_define([E2E_COMPARE_SYSCALLS], [E2E_COMPARE_CONTENT([$1], [syscalls])])
m4_define([E2E_COMPARE_STDOUT],   [E2E_COMPARE_CONTENT([$1], [stdout])])
m4_define([E2E_COMPARE_STDERR],   [E2E_COMPARE_CONTENT([$1], [stderr])])

AT_BANNER([e2e tests])

m4_include([e2e-syscalls-clrunimap.at])
m4_include([e2e-syscalls-getunimap.at])
m4_include([e2e-syscalls-kbd_mode.at])
m4_include([e2e-syscalls-kbdinfo.at])
m4_include([e2e-syscalls-loadkeys.at])
m4_include([e2e-syscalls-loadunimap.at])
m4_include([e2e-syscalls-setfont.at])
m4_include([e2e-syscalls-setvtrgb.at])

AT_BANNER([e2e misc])

m4_include([e2e-misc-psfxtable.at])
m4_include([e2e-misc-dumpkeys.at])
m4_include([e2e-misc-data-keymaps.at])

AT_BANNER([e2e memcheck])

m4_include([e2e-memcheck-clrunimap.at])
m4_include([e2e-memcheck-dumpkeys.at])
m4_include([e2e-memcheck-getunimap.at])
m4_include([e2e-memcheck-loadkeys.at])
m4_include([e2e-memcheck-loadunimap.at])
m4_include([e2e-memcheck-setfont.at])
m4_include([e2e-memcheck-setvtrgb.at])
